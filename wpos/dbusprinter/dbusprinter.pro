# File generated by kdevelop's qmake manager.
# -------------------------------------------
# Subdirectory relative to the main directory of the project: ./dbusprinter
# Destination is an application: dcopprinter

QT += core dbus sql
QT -= gui
CONFIG += c++17
#CONFIG += debug -Wno-narrowing warn_off qt
CONFIG += debug -Wall warn_on qt
#CONFIG += release warn_of qt
TEMPLATE = app
TARGET = dbusprinter

QMAKE_CXXFLAGS += -std=c++20 -Os -s
QMAKE_CXXFLAGS += -Wno-deprecated-copy  -Wno-narrowing

INSTALLS += etc usr target

target.path = /usr/bin
usr.files += usr/*
usr.path = /usr/share/dcopprinter
etc.files += etc/*
etc.path = /etc/ntpv

TARGETDEPS += \
    ../libqutexr/libqutexr.so \
    ../wposcore/libwposcore.so \
    ../libbslxml/libbslxml.so

INCLUDEPATH += \
    ../libqutexr \
    ../wposcore \
    ../libbslxml \
    /usr/include/libxml2 \
    ..

QMAKE_LIBDIR = \
    ../libqutexr \
    ../wposcore \
    ../libbslxml

LIBS += \
    -lqutexr \
    -lwposcore \
    -lbslxml \
    -lcups

DISTFILES += \
    etc/dtds/dcopprinter_config.dtd \
    etc/dcopprinter_config.xml \
    usr/replace.pl \
    AUTHORS \
    COPYING

HEADERS += \
    dbusprinter.h \
    dbusprinterinterface.h \
    main.h \
    printer.h \
    printermanager.h \
    printersamsungsrp350.h \
    printerticket.h \
    xmlrpcprinter.h

SOURCES += \
    dbusprinter.cpp \
    main.cpp \
    printer.cpp \
    printermanager.cpp \
    printersamsungsrp350.cpp \
    printerticket.cpp \
    xmlrpcprinter.cpp

DBUS_ADAPTORS += com.wiresens.dbusprinter.xml
DBUS_HEADER_FILES += dbusprinter.h

#DBUS compiler flags.
#export all the signals and public slots
DBUS_FLAGS = -M #export all public slots
DBUS_OUT_FLAGS = -o

# Newer versions of QtCreator do builds in a separate directory. As a
# result, we need to append the source directory to DBUS files.
#
for(file, DBUS_HEADER_FILES) {
  DBUS_PWD_FILES += $$PWD/$${file}
}

qdbuscpp2xml.name = qdbuscpp2xml ${QMAKE_FILE_IN}
qdbuscpp2xml.input = DBUS_PWD_FILES
qdbuscpp2xml.output = com.wiresens.${QMAKE_FILE_BASE}.xml
qdbuscpp2xml.commands = qdbuscpp2xml $$DBUS_FLAGS ${QMAKE_FILE_IN} $$DBUS_OUT_FLAGS com.wiresens.${QMAKE_FILE_BASE}.xml
qdbuscpp2xml.CONFIG = no_link
qdbuscpp2xml.depends = $$DBUS_HEADER_FILES
qdbuscpp2xml.clean = com.wiresens.${QMAKE_FILE_BASE}.xml
QMAKE_EXTRA_COMPILERS += qdbuscpp2xml

#include(../platform.pri)
#include(../destination.pri)

#DESTDIR     = $$PWD/../bin/$$DESTINATION_PATH
#OBJECTS_DIR = $$PWD/build/$$DESTINATION_PATH/.obj
#MOC_DIR     = $$PWD/build/$$DESTINATION_PATH/.moc
#message(wpostest project dir : $${PWD})
#message(wpostest output dir : $${DESTDIR})
