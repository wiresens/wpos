# File generated by kdevelop's qmake manager.
# -------------------------------------------
# Subdirectory relative to the main directory of the project: dbusreceipt
# Destination is an application: dbusreceipt

QT              -= gui
QT              += core dbus sql

CONFIG          += debug -Wall warn_on qt
#CONFIG         += release warn_off qt
TARGET          = dbusreceipt
TEMPLATE        = app

QMAKE_CXXFLAGS  += -std=c++20
QMAKE_CXXFLAGS  += -Wno-deprecated-copy  -Wno-narrowing

INSTALLS        += \
    etc \
    target

target.path     = /usr/bin
etc.files       += etc/*
etc.path        = /etc/ntpv

TARGETDEPS      += \
    ../libqutexr/libqutexr.so \
    ../wposcore/libwposcore.so \
    ../libbslxml/libbslxml.so

LIBS += \
    -lqutexr \
    -lwposcore \
    -lbslxml

QMAKE_LIBDIR = \
    ../libqutexr \
    ../wposcore \
    ../libbslxml

MOC_DIR      = moc
OBJECTS_DIR  = obj

INCLUDEPATH += \
    ../libqutexr \
    ../wposcore \
    ../libbslxml \
    /usr/include/libxml2 \
    ..

# To be generated by qdbuscpp2xml compiler
# The generated xml will serve as input for qdbusxml2cpp to generate *_adaptor.{h,cpp}
# qdbusxml2cpp is automatically invoqued by qmake
DBUS_ADAPTORS       += \
    com.wiresens.dbusreceiptprimitive.xml \
    com.wiresens.dbusreceiptmediator.xml

# Input files for qdbuscpp2xml to generate the above xml
# qdbuscpp2xml invocation is done bellow by adding custom compiler
# to qmake invoqued by qmake QMAKE_EXTRA_COMPILERS
DBUS_ADAPTOR_FILES += \
    dbusreceiptprimitive.h \
    dbusreceiptmediator.h

# QtDBUS qdbuscpp2xml compiler flags for generating xml specs from DBUS_ADAPTOR_FILES.
DBUS_FLAGS        = -M #export all public slots
DBUS_OUT_FLAGS    = -o # output option

# Newer versions of QtCreator do builds in a separate directory. As a
# result, we need to append the source directory to DBUS files.
#
for(file, DBUS_ADAPTOR_FILES) {
  DBUS_PWD_FILES += $$PWD/$${file}
}

qdbuscpp2xml.name       = qdbuscpp2xml ${QMAKE_FILE_IN}
qdbuscpp2xml.input      = DBUS_PWD_FILES
qdbuscpp2xml.output     = com.wiresens.${QMAKE_FILE_BASE}.xml
qdbuscpp2xml.commands   = qdbuscpp2xml $$DBUS_FLAGS ${QMAKE_FILE_IN} $$DBUS_OUT_FLAGS com.wiresens.${QMAKE_FILE_BASE}.xml
qdbuscpp2xml.CONFIG     = no_link
#qdbuscpp2xml.depends    = $$DBUS_ADAPTOR_FILES
qdbuscpp2xml.clean      = com.wiresens.${QMAKE_FILE_BASE}.xml
QMAKE_EXTRA_COMPILERS   += qdbuscpp2xml

HEADERS += \
    dbusreceiptmanager.h \
    dbusreceiptmediator.h \
    dbusreceiptprimitive.h \
    receiptchangenotifiyer.h \
    receiptclient.h \
    receiptclientmediator.h \
    receiptdb.h \
    receiptmediatorinterface.h \
    receiptprimitiveinterface.h

SOURCES += \
    dbusreceiptmanager.cpp \
    dbusreceiptmediator.cpp \
    dbusreceiptprimitive.cpp \
    receiptchangenotifiyer.cpp \
    receiptclient.cpp \
    receiptclientmediator.cpp \
    receiptdb.cpp \
    main.cpp

DISTFILES += \
    etc/dcopreceipt_config.xml \
    etc/dtds/dcopreceipt_config.dtd \
    AUTHORS \
    COPYING \
    README
