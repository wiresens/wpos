# File generated by kdevelop's qmake manager and Manually modified by
# Gilles Bene.
# -------------------------------------------
# Subdirectory relative to the main directory of the project: .
# Target is a project subdirectory
# This project builds 7 applications and 4 libraries that integrates to provides the
# wPOS POS Software

# Applications :
#   * wpos          : POS front office
#   * wposbo        : POS back office
#   * xmlmanage     : cmdline app for parsing xml
#   * dbusstick     : for database backup on usb
#   * dbusprinter   : for receit printing
#   * dbusreports   : for reporting
#   * dbusreceipt   : for receit managemenet
#   * wposrpcd      : RPC Deamon for receit managemenet

#Libraries
#   * wposxml     : xml parsing API
#   * wposcore       : wPOS database access and management
#   * wposbwidget : Widget for the wPOS GUI.
#   * wposqutexr     : lightweight XML-RPC client and server implemantation

TEMPLATE = subdirs
DISTFILES += \
    AUTHORS \
    COPYING \
    README

#projects
SUBDIRS += \
    libbslxml \
    dbusreceipt \
    domainmodel \
    wposcore \
    wposwidget \
    libqutexr \
    xmlmanage  \
    dbusprinter \
    wpos \
    wposbo \
    test \
#    wposrpcd \
#    dcopstick \
#    dcopreports \

#define project dependencies to enable for astonishing build parallelism
wposcore.depends   = libbslxml
libqutexr.depends  = libbslxml
xmlmanage.depends  = libbslxml
wposrpcd.depends   = libbslxml libqutexr
wpos.depends       = domainmodel libbslxml wposcore wposwidget libqutexr
wposbo.depends     = domainmodel libbslxml wposcore wposwidget
wposwidget.depends = libbslxml wposcore dbusprinter

#Left for later port because relying on DCOP(Desktop Communications Protocol)
#Need a port to D-BUS(Desktop Bus) all are IPC for desktop application communication
dbusprinter.depends = libbslxml wposcore libqutexr
dbusreceipt.depends = libbslxml wposcore libqutexr
dcopstick.depends = libbslxml

message("----------------------------------------")
message("nTPV ")
message("----------------------------------------")
message("Author : Carlos Manzanedo <carlos@napsis.com>")
message("Author : Pedro Angel Gonzalez <pedro@napsis.com>")
message("Homepage: http://www.napsis.com")
message("License: MIT ")
message("Improvement: Gilles Bene <gilles.benepougoue@gmail.com>")
message("----------------------------------------")
